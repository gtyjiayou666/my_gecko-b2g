/* -*- Mode: C++; tab-width: 8; indent-tabs-mode: nil; c-basic-offset: 2 -*- */
/* vim: set sw=2 ts=8 et ft=cpp : */
/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this file,
 * You can obtain one at http://mozilla.org/MPL/2.0/. */

include protocol PContent;
include protocol PMobileConnectionRequest;
include PMobileConnectionTypes;

include "mozilla/dom/mobileconnection/MobileConnectionChild.h";
include "mozilla/dom/mobileconnection/MobileConnectionParent.h";

namespace mozilla {
namespace dom {
namespace mobileconnection {

[ManualDealloc, ChildImpl="MobileConnectionChild", ParentImpl="MobileConnectionParent"]
sync protocol PMobileConnection
{
  manager PContent;
  manages PMobileConnectionRequest;

child:
  async NotifyVoiceInfoChanged(nsMobileConnectionInfo aInfo);
  async NotifyDataInfoChanged(nsMobileConnectionInfo aInfo);
  async NotifyDataError(nsString aMessage);
  async NotifyCFStateChanged(uint16_t aAction, uint16_t aReason, nsString aNumber,
                             uint16_t aTimeSeconds, uint16_t aServiceClass);
  async NotifyEmergencyCbModeChanged(bool aActive, uint32_t aTimeoutMs);
  async NotifyOtaStatusChanged(nsString aStatus);
  async NotifyRadioStateChanged(int32_t aRadioState);
  async NotifyClirModeChanged(uint32_t aMode);
  async NotifyLastNetworkChanged(nsString aNetwork);
  async NotifyLastHomeNetworkChanged(nsString aNetwork);
  async NotifyNetworkSelectionModeChanged(int32_t aMode);
  async NotifySignalStrengthChanged(nsMobileSignalStrength aSignalStrength);
  async NotifyModemRestart(nsString aReason);
  async NotifyScanResultReceived(nsMobileNetworkInfo[] aScanResults);
  async NotifyDeviceIdentitiesChanged(nsMobileDeviceIdentities aDeviceIdentities);

parent:
  /**
   * Send when child no longer needs to use PMobileConnection.
   */
  async __delete__();

  /**
   * Sent when the child makes an asynchronous request to the parent.
   */
  async PMobileConnectionRequest(MobileConnectionRequest aRequest);

  /**
   * Sync call only be called once per child actor for initialization.
   */
  sync Init()
    returns (nsMobileConnectionInfo aVoice, nsMobileConnectionInfo aData,
             nsString aLastKnownNetwork, nsString aLastKnownHomeNetwork,
             int32_t aNetworkSelectionMode, int32_t aRadioState,
             int32_t[] aSupportedNetworkTypes, bool aEmergencyCbMode,
             nsMobileSignalStrength aSignalStrength,
             nsMobileDeviceIdentities aDeviceIdentities);

  sync GetSupportedNetworkTypes()
    returns (int32_t[] aSupportedNetworkTypes);
};

/**
 * MobileConnectionRequest
 */
struct GetNetworksRequest
{
};

struct SelectNetworkRequest
{
  nsMobileNetworkInfo network;
};

struct SelectNetworkAutoRequest
{
};

struct SetPreferredNetworkTypeRequest
{
  int32_t type;
};

struct GetPreferredNetworkTypeRequest
{
};

struct SetRoamingPreferenceRequest
{
  int32_t mode;
};

struct GetRoamingPreferenceRequest
{
};

struct SetVoNrEnabledRequest
{
  bool enabled;
};

struct IsVoNrEnabledRequest
{
};

struct SetNrDualConnectivityStateRequest
{
  int32_t mode;
};

struct IsNrDualConnectivityEnabledRequest
{
};

struct StartNetworkScanRequest
{
  int32_t scanType;
  int32_t interval;
  int32_t maxSearchTime;
  bool incrementalResults;
  int32_t incrementalResultsPeriodicity;
  nsString mccMncs;
  nsGeckoRadioAccessSpecifier[] specifiers;
};


struct SetVoicePrivacyModeRequest
{
  bool enabled;
};

struct GetVoicePrivacyModeRequest
{
};

struct SetCallForwardingRequest
{
  uint16_t action;
  uint16_t reason;
  nsString number;
  uint16_t timeSeconds;
  uint16_t serviceClass;
};

struct GetCallForwardingRequest
{
  uint16_t reason;
  uint16_t serviceClass;
};

struct SetCallBarringRequest
{
  uint16_t program;
  bool enabled;
  nsString password;
  uint16_t serviceClass;
};

struct GetCallBarringRequest
{
  uint16_t program;
  nsString password;
  uint16_t serviceClass;
};

struct ChangeCallBarringPasswordRequest
{
  nsString pin;
  nsString newPin;
};

struct SetCallWaitingRequest
{
  bool enabled;
  uint16_t serviceClass;
};

struct GetCallWaitingRequest
{
};

struct SetCallingLineIdRestrictionRequest
{
  uint16_t mode;
};

struct GetCallingLineIdRestrictionRequest
{
};

struct ExitEmergencyCbModeRequest
{
};

struct SetRadioEnabledRequest
{
  bool enabled;
  bool forEmergencyCall;
  bool preferredForEmergencyCall;
};

struct GetDeviceIdentitiesRequest
{
};

struct GetSignalStrengthRequest
{
};

struct StopNetworkScanRequest
{
};

union MobileConnectionRequest
{
  GetNetworksRequest;
  SelectNetworkRequest;
  SelectNetworkAutoRequest;
  SetPreferredNetworkTypeRequest;
  GetPreferredNetworkTypeRequest;
  SetRoamingPreferenceRequest;
  GetRoamingPreferenceRequest;
  SetVoNrEnabledRequest;
  IsVoNrEnabledRequest;
  SetNrDualConnectivityStateRequest;
  IsNrDualConnectivityEnabledRequest;
  SetVoicePrivacyModeRequest;
  GetVoicePrivacyModeRequest;
  SetCallForwardingRequest;
  GetCallForwardingRequest;
  SetCallBarringRequest;
  GetCallBarringRequest;
  ChangeCallBarringPasswordRequest;
  SetCallWaitingRequest;
  GetCallWaitingRequest;
  SetCallingLineIdRestrictionRequest;
  GetCallingLineIdRestrictionRequest;
  ExitEmergencyCbModeRequest;
  SetRadioEnabledRequest;
  GetDeviceIdentitiesRequest;
  GetSignalStrengthRequest;
  StopNetworkScanRequest;
  StartNetworkScanRequest;
};

} // namespace mobileconnection
} // namespace dom
} // namespace mozilla
